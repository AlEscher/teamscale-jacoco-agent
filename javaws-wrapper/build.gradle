plugins {
	id "com.github.johnrengelman.shadow" version "1.2.3"
	id 'edu.sc.seis.launch4j' version '2.4.3'
}

apply plugin: 'java'
apply plugin: 'application'

dependencies {
  	compile 'org.apache.commons:commons-lang3:3.7'
	compile project(':eu.cqse.teamscale.jacoco.client')
}

mainClassName = "eu.cqse.teamscale.jacoco.javaws.Main"
shadowJar {
    baseName = 'teamscale-javaws-wrapper'
    classifier = null
    version = rootProject.appVersion
}

launch4j {
	outfile = 'javaws.exe'
  	mainClassName = 'eu.cqse.teamscale.jacoco.javaws.Main'
  	
  	// make it work with shadow jar
	copyConfigurable = project.tasks.shadowJar.outputs.files
	jar = "lib/${project.tasks.shadowJar.archiveName}"
}

task doc(type: Exec) {
    doFirst {
        mkdir 'build/doc'
    }
    commandLine 'pandoc', 'README.md', '-o', 'build/doc/README.pdf'
}

task dist(type: Zip) {
    dependsOn createExe, doc
    
    doFirst {
    	file('build/version.txt').withWriter { it.println(rootProject.appVersion) }
    }
    
    version rootProject.appVersion
    from('build/launch4j') {
    	include '*.exe'
    }
	from(project(':eu.cqse.teamscale.jacoco.client').tasks.shadowJar.archivePath) {
		rename(/.*/, 'agent.jar')
	}
    from 'resources'
    from 'build/doc'
    from 'build/version.txt'
    from(project(':eu.cqse.teamscale.jacoco.client').projectDir.toString() + '/build/doc/USAGE.pdf') {
    	rename(/.*/, 'agent-userguide.pdf')
    }
}

