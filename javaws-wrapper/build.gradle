import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
	id 'com.teamscale.java-convention'
	id 'de.fntsoftware.gradle.markdown-to-pdf'
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow'
	id 'com.teamscale.coverage'
}

dependencies {
	implementation 'com.teamscale:teamscale-lib-commons:8.3.2'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.commons:commons-exec:1.3'
}

mainClassName = 'com.teamscale.jacoco.javaws.Main'

shadowJar {
	archiveBaseName.set('teamscale-javaws-wrapper')
	archiveClassifier.set(null)
	archiveVersion.set(rootProject.appVersion)
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
	target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

startShadowScripts {
	applicationName = 'javaws'
}

distributions {
	shadow {
		distributionBaseName = 'javaws'
		contents {
			from(readmeToPdf) {
				into 'documentation'
				rename 'README.pdf', 'userguide.pdf'
			}

			from(project(':agent').tasks.readmeToPdf) {
				rename('README.pdf', 'agent-userguide.pdf')
				into 'documentation'
			}

			from(project(':agent').tasks.shadowJar) {
				rename(/.*/, 'agent.jar')
			}
		}
	}
}

repositories {
	mavenCentral()
}

tasks.named('shadowDistZip').configure {
	archiveFileName = 'javaws.zip'
}
