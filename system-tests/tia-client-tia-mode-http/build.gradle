plugins {
	id 'java'
	// we do not enable code coverage recording for the system tests as we already need our agent attached
	// it would conflict with JaCoCo's
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

test {
	testLogging.exceptionFormat "full"
	useJUnitPlatform()

	/** These ports must match what is configured in the SystemTest class. */
	def agentPort = 65433
	def agentJar = project(':agent').tasks.shadowJar.outputs.files.files.first()
	jvmArgs "-javaagent:$agentJar=http-server-port=$agentPort,tia-mode=http," +
			"mode=testwise,includes=*systemundertest.*"
}

dependencies {
	implementation project(':tia-client')

	testImplementation 'com.sparkjava:spark-core:2.9.3'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
	testImplementation 'org.assertj:assertj-core:3.19.0'
	testImplementation 'com.squareup.moshi:moshi:1.12.0'
}

tasks.test.dependsOn(project(':agent').tasks.shadowJar)
