plugins {
	id 'org.jetbrains.kotlin.jvm' version "1.7.21"
	id 'com.teamscale.java-convention'
	id 'java-gradle-plugin'
	id 'com.teamscale.coverage'
	id 'com.teamscale.publish'
	id "com.gradle.plugin-publish" version "1.1.0"
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(11))
	}
}

publishAs {
	readableName = "Teamscale Gradle Plugin"
	description = "A Gradle plugin that supports collecting Testwise Coverage and uploading reports to Teamscale."
}

pluginBundle {
	website = 'https://www.teamscale.com/'
	vcsUrl = 'https://github.com/cqse/teamscale-jacoco-agent'
	tags = ['teamscale', 'coverage', 'tga', 'test', 'gap', 'junit', 'upload']
}

gradlePlugin {
	plugins {
		teamscalePlugin {
			id = "com.teamscale"
			displayName = 'Teamscale Gradle plugin'
			implementationClass = "com.teamscale.TeamscalePlugin"
			description = 'Supports collecting Testwise Coverage and uploading reports to Teamscale.'
		}
	}
}

dependencies {
	implementation project(':teamscale-client')
	implementation project(':report-generator')
	implementation gradleApi()
	implementation 'org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r'
	implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
	testImplementation 'com.squareup.okio:okio:3.2.0'
}

processResources {
	inputs.property("version", version)
	filesMatching('**/plugin.properties') {
		filter {
			it.replace('%PLUGIN_VERSION_TOKEN_REPLACED_DURING_BUILD%', version)
		}
	}
}

test {
	dependsOn ':agent:publishToMavenLocal'
	dependsOn ':impacted-test-engine:publishToMavenLocal'
	dependsOn ':teamscale-client:publishToMavenLocal'
	dependsOn ':tia-client:publishToMavenLocal'
	dependsOn ':report-generator:publishToMavenLocal'
}
