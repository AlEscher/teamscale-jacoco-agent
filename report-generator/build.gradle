plugins {
	id 'com.github.johnrengelman.shadow' version '4.0.1'
	id 'java-library'
	id 'maven'
}

version reportGeneratorVersion

configurations {
	conqat
	implementation.extendsFrom(conqat)
}

dependencies {
	implementation project(':teamscale-client')

	implementation "org.jacoco:org.jacoco.core:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.report:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"

	implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
	implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"

	conqat 'org.conqat:org.conqat.lib.commons:0.20160822'

	implementation 'com.google.code.gson:gson:2.8.5'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.assertj:assertj-core:3.8.0'
	testImplementation 'org.mockito:mockito-core:2.19.0'
}

test {
	testLogging.exceptionFormat "full"
}

// Needed to bundle org.conqat.lib.commons into the report-generator
// as this dependency is not available via mavenCentral
shadowJar {
	classifier = null
	configurations = [project.configurations.conqat]
}

install {
	repositories.mavenInstaller {
		configuration = configurations.shadow
//		pom.whenConfigured { it.dependencies.clear() }
	}
}

// At the moment we are stuck with the old maven plugin until support for private key
// files is added or we add a dedicated user with a password to our server.
// https://github.com/gradle/gradle/issues/1263
uploadShadow {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			repository(url: cqseMavenRepositoryUrl) {
				authentication(userName: cqseMavenRepositoryUser, privateKey: cqseMavenRepositoryPrivateKey)
			}
//			pom.whenConfigured { it.dependencies.clear() }
		}
	}
}
