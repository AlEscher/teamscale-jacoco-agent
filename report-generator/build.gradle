plugins {
	id 'java-library'
	id 'maven'
}

version reportGeneratorVersion

dependencies {
	implementation project(':teamscale-client')

	implementation "org.jacoco:org.jacoco.core:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.report:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"

	implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
	implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"

	implementation 'org.conqat:org.conqat.lib.commons:0.20160822'

	implementation 'com.google.code.gson:gson:2.8.5'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.assertj:assertj-core:3.8.0'
	testImplementation 'org.mockito:mockito-core:2.19.0'
}

test {
	testLogging.exceptionFormat "full"
}

install {
	repositories.mavenInstaller {
		configurePom(pom)
	}
}

private void configurePom(pom) {
	pom.withXml {
		asNode().remove(asNode().get('dependencies'))
		def dep = asNode().appendNode('dependecies').appendNode('dependency')
		dep.appendNode('groupId', 'com.teamscale')
		dep.appendNode('artifactId', 'teamscale-client')
		dep.appendNode('version', teamscaleClientVersion)
		dep.appendNode('scope', 'compile')
	}
}

// At the moment we are stuck with the old maven plugin until support for private key
// files is added or we add a dedicated user with a password to our server.
// https://github.com/gradle/gradle/issues/1263
uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			repository(url: cqseMavenRepositoryUrl) {
				authentication(userName: cqseMavenRepositoryUser, privateKey: cqseMavenRepositoryPrivateKey)
			}
		}
	}
}