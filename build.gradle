plugins {
	id 'com.github.ben-manes.versions'
}

ext {
	appVersion = '22.2.0'

	// When upgrading JaCoCo to a newer version make sure to
	// check the comment in the AnalyzerCache.java and CachingInstructionsBuilder.java
	// and update the internal_xxxxxx hash included in the PreMain package name in AgentBase.java.
	jacocoVersion = '0.8.7'
}

subprojects {
	if (VersionUtils.INSTANCE.isTaggedRelease()) {
		version appVersion
	} else {
		version appVersion + '-SNAPSHOT'
	}
}

// To install the artifacts locally run publishToMavenLocal

// Installs the plugin to your local maven repository without the agent
task iGP(dependsOn: [
		':impacted-test-engine:publishToMavenLocal',
		':report-generator:publishToMavenLocal',
		':teamscale-gradle-plugin:publishToMavenLocal',
		':teamscale-client:publishToMavenLocal',
		':tia-client:publishToMavenLocal'
])

dependencyUpdates.resolutionStrategy {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'b', 'rc', 'cr', 'm', 'preview', 'pr'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}
