plugins {
	alias(libs.plugins.versions)
	alias(libs.plugins.nexusPublish)
}

group = "com.teamscale"

ext {
	appVersion = '29.1.3'

	jacocoVersion = libs.versions.jacoco.get()

	if (VersionUtils.INSTANCE.isTaggedRelease()) {
		snapshotVersion = appVersion
	} else {
		snapshotVersion = appVersion + '-SNAPSHOT'
	}
}

allprojects {
	version snapshotVersion
}

// Installs all Maven artifacts to your local Maven repository
task publishToMavenLocal
subprojects {
	// must be run after evaluation because the publishToMavenLocal tasks are generated by our plugin during
	// project evaluation
	afterEvaluate {
		def publishTask = it.tasks.findByPath("publishToMavenLocal")
		if (publishTask != null) {
			rootProject.tasks.publishToMavenLocal.dependsOn(publishTask)
		}
	}
}

nexusPublishing {
	repositories {
		sonatype()
	}
}

dependencyUpdates.resolutionStrategy {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'b', 'rc', 'cr', 'm', 'preview', 'pr'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}
