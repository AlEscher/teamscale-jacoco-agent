plugins {
	id 'com.teamscale.java-convention'
	id 'de.fntsoftware.gradle.markdown-to-pdf'
	id 'application'

	// we don't want to cause conflicts between our dependencies and the target application
	// since the agent will be loaded with the same class loader as the profiled application
	// so we use the shadow plugin to relocate our dependencies
	id 'com.teamscale.shadow-convention'
	id 'com.teamscale.coverage'
	id 'com.teamscale.publish'
}

publishAs {
	artifactId = "teamscale-jacoco-agent"
	readableName = "Teamscale JaCoCo Agent"
	description = "JVM profiler that simplifies various aspects around recording and uploading test coverage"
}

ext {
	outputVersion = rootProject.appVersion + '-jacoco-' + jacocoVersion
	jettyVersion = '9.4.28.v20200408'
	jerseyVersion = '2.31'
}

dependencies {

	implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
	implementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"

	implementation "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
	implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}"
	implementation "org.glassfish.jersey.containers:jersey-container-jetty-http:${jerseyVersion}"
	implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}"
	implementation "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"

	implementation project(':teamscale-client')
	implementation project(':report-generator')

	implementation "org.jacoco:org.jacoco.core:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.report:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"

	// We need to stay on the 1.3.x release line as 1.4.x requires Java 11
	implementation 'ch.qos.logback:logback-core:1.3.4'
	implementation 'ch.qos.logback:logback-classic:1.3.4'

	implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
	implementation 'com.beust:jcommander:1.82'
	implementation 'com.teamscale:teamscale-lib-commons:8.4.2'

	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.sparkjava:spark-core:2.9.4'

	implementation 'com.squareup.moshi:moshi:1.14.0'

	testImplementation project(':tia-client')
	testImplementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
	testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
}

mainClassName = 'com.teamscale.jacoco.agent.Main'

shadowJar {
	// since this is used as an agent, we want it to always have the same name
	// otherwise people have to adjust their -javaagent parameters after every
	// update
	archiveFileName.set('teamscale-jacoco-agent.jar')
	manifest {
		attributes 'Premain-Class': 'com.teamscale.jacoco.agent.PreMain'
	}
}

startShadowScripts {
	applicationName = 'convert'
}

distributions {
	shadow {
		distributionBaseName = 'teamscale-jacoco-agent'
		contents {
			from(readmeToPdf) {
				into 'documentation'
				rename 'README.pdf', 'userguide.pdf'
			}

			filesMatching('**/VERSION.txt') {
				filter {
					it.replace('%APP_VERSION_TOKEN_REPLACED_DURING_BUILD%', outputVersion)
				}
			}
		}
	}
}

tasks.named('shadowDistZip').configure {
	archiveFileName = 'teamscale-jacoco-agent.zip'
}

processResources {
	filesMatching('**/app.properties') {
		filter {
			it.replace('%APP_VERSION_TOKEN_REPLACED_DURING_BUILD%', rootProject.appVersion)
		}
	}
}
